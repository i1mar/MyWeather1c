
Функция ПолучитьДанныеОПогоде(Город, Дата, ПроизошлаОшибка, ТекстОшибки) Экспорт 

	Соединение=Новый HTTPСоединение("sinoptik.com.ru", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL);
	ТекстЗапроса = ПолучитьТекстЗапроса(Город, Дата);
	Запрос = Новый HTTPЗапрос(ТекстЗапроса);
	Ответ = Соединение.Получить(Запрос);

	МассивФильтров = ОбщегоНазначенияСервер.СформироватьМассивФильтров();
	
	Если Ответ.КодСостояния = 200 Тогда

		СтрокаHTML = Ответ.ПолучитьТелоКакСтроку();

		Чтение = Новый ЧтениеHTML;
		Чтение.УстановитьСтроку(СтрокаHTML);
		Построитель = Новый ПостроительDOM;

		ДокHTML = Построитель.Прочитать(Чтение);
		ДокHTML.НормализоватьДокумент();

		МассивУзловТекТемпература = ДокHTML.НайтиПоФильтру(МассивФильтров[0]);
		Если Не МассивУзловТекТемпература = Неопределено И МассивУзловТекТемпература.Количество() > 0 Тогда
			ТекущаяТемпература = СокрЛП(МассивУзловТекТемпература[0].ТекстовоеСодержимое);
		КонецЕсли;

		МассивУзловТекОписание = ДокHTML.НайтиПоФильтру(МассивФильтров[1]);
		Если Не МассивУзловТекОписание = Неопределено И МассивУзловТекОписание.Количество() > 0 Тогда
			Описание = СокрЛП(МассивУзловТекОписание[0].ТекстовоеСодержимое);
		КонецЕсли;

		МассивУзловТаблицаТемператур  = ДокHTML.НайтиПоФильтру(МассивФильтров[2]);
		МассивУзловТаблицаЧувствуется = ДокHTML.НайтиПоФильтру(МассивФильтров[3]);
		МассивУзловТаблицаВетер       = ДокHTML.НайтиПоФильтру(МассивФильтров[4]);

		СтруктураВозвращаемыхДанных = Новый Структура("Температура,Ощущается,Ветер");
		Если Не МассивУзловТаблицаТемператур = Неопределено И МассивУзловТаблицаТемператур.Количество() > 0 Тогда
			СтруктураВозвращаемыхДанных.Температура = ПеренестиДанныеВМассив(МассивУзловТаблицаТемператур);
		КонецЕсли;

		Если Не МассивУзловТаблицаЧувствуется = Неопределено И МассивУзловТаблицаЧувствуется.Количество() > 0 Тогда
			СтруктураВозвращаемыхДанных.Ощущается = ПеренестиДанныеВМассив(МассивУзловТаблицаЧувствуется);
		КонецЕсли;

		Если Не МассивУзловТаблицаВетер = Неопределено И МассивУзловТаблицаВетер.Количество() > 0 Тогда
			СтруктураВозвращаемыхДанных.Ветер = ПеренестиДанныеВМассив(МассивУзловТаблицаВетер,Истина);
		КонецЕсли;

	ИначеЕсли Ответ.КодСостояния = 404 Тогда
		ПроизошлаОшибка = Истина;
		ТекстОшибки = "Указнный город не найден";
	Иначе
		ПроизошлаОшибка = Истина;
		ТекстОшибки =  "Не удалось установить соединение";
	КонецЕсли;

	Возврат СтруктураВозвращаемыхДанных;
КонецФункции

Функция ПолучитьТекстЗапроса(Город, Дата)
	Возврат ?(ЭтоСегодняшняяДата(Дата), СтрШаблон("/погода-%1", Город), СтрШаблон("/погода-%1/%2", Город, Формат(Дата,
		"ДФ=yyyy-MM-dd;")));

КонецФункции

Функция ЭтоСегодняшняяДата(Дата)
	Возврат НачалоДня(Дата) = НачалоДня(ТекущаяДата());
КонецФункции

Функция ПеренестиДанныеВМассив(МассивВходящихДанных,НужнаДопОбработкаПредставления = Ложь)

	ВложенныйМассив = Новый Массив;
	Для Каждого тСтрока Из МассивВходящихДанных Цикл
		
		Если НужнаДопОбработкаПредставления Тогда
			
			МассивПредставлений = СтрРазделить(СокрП(тСтрока.ТекстовоеСодержимое),Символы.ПС);
			ТекстовоеСодержимое = МассивПредставлений[МассивПредставлений.ВГраница()];
			
		Иначе
			ТекстовоеСодержимое = СокрЛП(тСтрока.ТекстовоеСодержимое);
		КонецЕсли;
		ВложенныйМассив.Добавить(ТекстовоеСодержимое);
		
	КонецЦикла;

	Возврат ВложенныйМассив;

КонецФункции